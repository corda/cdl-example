import static org.gradle.api.JavaVersion.VERSION_11

plugins {
    id 'org.jetbrains.kotlin.jvm' apply false
    id 'net.corda.cordapp.cordapp-configuration'
}

subprojects {
    //Properties that you need to compile your project (The application)
    pluginManager.withPlugin('org.jetbrains.kotlin.plugin.noarg') {
        noArg {
            annotations(
                'javax.persistence.Entity',
                'javax.persistence.Embeddable',
                'javax.persistence.MappedSuperclass'
            )
        }
    }

    pluginManager.withPlugin('org.jetbrains.kotlin.plugin.allopen') {
        allOpen {
            annotations(
                'javax.persistence.Entity',
                'javax.persistence.Embeddable',
                'javax.persistence.MappedSuperclass'
            )
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            languageVersion = '1.4'
            apiVersion = '1.4'
            jvmTarget = VERSION_11
            javaParameters = true   // Useful for reflection.
            freeCompilerArgs += [
                '-Xjvm-default=all',
                // Prevent Kotlin from warning about kotlin.* classes inside the OSGi bundle.
                '-Xskip-runtime-version-check'
            ]
        }
    }

    tasks.withType(Jar).configureEach {
        // This makes the JAR's SHA-256 hash repeatable.
        preserveFileTimestamps = false
        reproducibleFileOrder = true
    }
}
